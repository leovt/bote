%ignore /[ \n\t]/

// the following rules will be defined by the program reading this grammar.
// these are just sample rules
// keyword: /flying|crush|readiness|cannot_block|cannot_attack/

color: /red|green|blue|yellow|white/
type: /player|permanent|creature|enchantment|source/
subtype: /goblin|dragon|elemental|minotaur|human/
energy: /(\{([0-9]+|[RGBWY])\})+/
number: /[0-9]+/
signed_number: /[\+\-][0-9]+/
variable: "X"
signed_variable: /[\+\-]X/
_numeric: number | variable
_signed_numeric: signed_number | signed_variable

start: effects

effects: _effect (";" _effect)* [";"]

_effect: damage_effect
       | add_energy_effect
       | destruction_effect
       | continuous_effect

damage_effect: _object "gets" _numeric "damage"
destruction_effect: "destroy" _object
add_energy_effect: "add" energy "to" _object "energy pool"
continuous_effect: _object "has" modifier_list [until_end_of_turn]

until_end_of_turn: "until end of turn"

modifier_list: modifier ("and" modifier)*

modifier: _signed_numeric "/" _signed_numeric -> delta_stat
        | _numeric "/" _numeric -> set_stat
        | keyword -> add_keyword
        | "not" keyword -> remove_keyword
        | "controlled by" _object -> change_controller

_object: all | this | effect_controller | _chosen | controller_of | enchanted

all: "all" selector
this: "this"
effect_controller: "you"
_chosen: chosen_spec | chosen_ref

chosen_spec: "chosen" selector [chosen_label]
chosen_ref: _chosen_ref_label
chosen_label: _chosen_ref_label
 _chosen_ref_label: "[" /[\w]+/ "]"

enchanted: "enchanted" selector [chosen_label]

selector: selector_term+

selector_term: "." _spec -> positive_selector
             | "!" _spec -> negative_selector

_spec: type_spec | color_spec | other_spec | subtype_spec

color_spec: "color" "(" color ("," color)* ")"
          | color

type_spec: "type" "(" type ("," type)* ")"
          | type

subtype_spec: "subtype" "(" subtype ("," subtype)* ")"
          | subtype

other_spec: "other"

controller_of: "controller of" _object
