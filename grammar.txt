%ignore /[ \n\t]/

start: effects

selector: selector_term+

selector_term: "." _spec -> positive_selector
             | "!" _spec -> negative_selector

_spec: type_spec | color_spec

color_spec: "color" "(" color ("," color)* ")"
          | color

color: /red|green|blue|yellow|white/

type_spec: "type" "(" type ("," type)* ")"
          | type

type: /player|permanent|creature|enchantment|source/

keyword: /flying|trample|haste|cannot_block|cannot_attack/

until_end_of_turn: "until end of turn"

modifier: stat_mod
        | keyword -> add_keyword
        | "not" keyword -> remove_keyword
        | "controlled by" _player_ref -> change_controller

stat_mod: num_mod "/" num_mod

num_mod: "+" number -> increase_stat
       | "-" number -> decrease_stat
       | number -> set_stat

_player_ref: effect_controller | _chosen

effect_controller: "you"

_chosen: chosen_spec
       | chosen_ref

chosen_spec: "chosen" selector [chosen_label]

chosen_ref: _chosen_ref_label
chosen_label: _chosen_ref_label
 _chosen_ref_label: "[" /[\w]+/ "]"

all: "all" selector

_object: _chosen | all

energy: energy_spec+

energy_spec: number -> en_colorless
            | "r" -> en_red
            | "g" -> en_green
            | "b" -> en_blue
            | "w" -> en_white
            | "y" -> en_yellow

continuous_effect: _object "has" modifier ("and" modifier)* [until_end_of_turn]

number: /[0-9]+/

damage_effect: _chosen "gets" number "damage"

add_energy_effect: "add" energy "to" _player_ref "energy pool"

effects: _effect (";" _effect)*

_effect: continuous_effect
       | damage_effect
       | add_energy_effect
