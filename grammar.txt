%ignore /[ \n\t]/

color: /red|green|blue|yellow|white/
type: /player|permanent|creature|enchantment|source/
subtype: /goblin|dragon|elemental|minotaur|human/
keyword: /flying|trample|haste|cannot_block|cannot_attack/
energy: /(\{([0-9]+|[RGBWY])\})+/
number: /[0-9]+/

start: effects

effects: _effect (";" _effect)* [";"]

_effect: damage_effect
       | add_energy_effect
       | destruction_effect
       | continuous_effect

damage_effect: _object "gets" number "damage"
destruction_effect: "destroy" _object
add_energy_effect: "add" energy "to" _object "energy pool"
continuous_effect: _object "has" modifier_list [until_end_of_turn]

until_end_of_turn: "until end of turn"

modifier_list: modifier ("and" modifier)*

modifier: _stat_mod -> stat_mod
        | keyword -> add_keyword
        | "not" keyword -> remove_keyword
        | "controlled by" _object -> change_controller

_stat_mod: num_mod "/" num_mod

num_mod: "+" number -> increase_stat
       | "-" number -> decrease_stat
       | number -> set_stat

_object: all | self | effect_controller | _chosen

all: "all" selector
self: "self"
effect_controller: "you"
_chosen: chosen_spec | chosen_ref

chosen_spec: "chosen" selector [chosen_label]
chosen_ref: _chosen_ref_label
chosen_label: _chosen_ref_label
 _chosen_ref_label: "[" /[\w]+/ "]"

selector: selector_term+

selector_term: "." _spec -> positive_selector
             | "!" _spec -> negative_selector

_spec: type_spec | color_spec | other_spec | subtype_spec

color_spec: "color" "(" color ("," color)* ")"
          | color

type_spec: "type" "(" type ("," type)* ")"
          | type

subtype_spec: "subtype" "(" subtype ("," subtype)* ")"
          | subtype

other_spec: "other"
